\ From: John Hayes S1I
\ Subject: tester.fr
\ Date: Mon, 27 Nov 95 13:10:09 PST

\ (C) 1995 JOHNS HOPKINS UNIVERSITY / APPLIED PHYSICS LABORATORY
\ MAY BE DISTRIBUTED FREELY AS LONG AS THIS COPYRIGHT NOTICE REMAINS.
\ VERSION 1.1

\ 22/1/09 The words { and } have been changed to T{ and }T respectively to
\ agree with the Forth 200X file ttester.fs. This avoids clashes with
\ locals using { ... } and the FSL use of }

\ 3/9/18 Added TALKING to switch VERBOSE, TESTING uses SRCLINE

.( loading TESTER -- J.Hayes ) cr

DECIMAL

HIDE

 0 CONSTANT FALSE
-1 CONSTANT TRUE

\ SET THE FOLLOWING FLAG TO TRUE FOR MORE VERBOSE OUTPUT; THIS MAY
\ ALLOW YOU TO TELL WHICH TEST CAUSED YOUR SYSTEM TO HANG.
VARIABLE VERBOSE
   FALSE VERBOSE !
\ TRUE VERBOSE !

: EMPTY-STACK ( ... -- ) \ EMPTY STACK: HANDLES UNDERFLOWED STACK TOO.
   DEPTH ?DUP
          IF DUP 0< IF NEGATE 0
            DO 0 LOOP
          ELSE 0 DO DROP LOOP THEN
          THEN
FLOATS? [IF]
   FDEPTH ?DUP
	  IF DUP 0< IF NEGATE 0
            DO 0.0e LOOP
          ELSE 0 DO FDROP LOOP THEN
          THEN
[THEN]
;

VARIABLE LASTLINE
: .LASTLINE     \ DISPLAY LINE CORRESPONDING TO ERROR
   SOURCE DROP LASTLINE @ CHARS +
   >IN @  LASTLINE @ - 1+ TYPE ;

: ERROR     \ ( C-ADDR U -- ) DISPLAY AN ERROR MESSAGE FOLLOWED BY
            \ THE LINE THAT HAD THE ERROR.
   TYPE .LASTLINE CR  	\ DISPLAY LINE CORRESPONDING TO ERROR
   EMPTY-STACK          \ THROW AWAY EVERY THING ELSE
\   QUIT                \ *** Uncomment this line to QUIT on an error
;

VARIABLE ACTUAL-DEPTH   \ STACK RECORD
CREATE ACTUAL-RESULTS 32 CELLS ALLOT

FLOATS? [IF]
VARIABLE ACTUAL-FDEPTH  \ FSTACK RECORD
CREATE ACTUAL-FRESULTS 32 FLOATS ALLOT
[THEN]

SET-CURRENT

: T{ ( -- )
\G Syntactic sugar.
   >IN @ 3 CHARS - LASTLINE !
   ;

: -> ( ... -- )
\G RECORD DEPTH AND CONTENT OF STACK.
   DEPTH DUP ACTUAL-DEPTH !   \ RECORD DEPTH
   ?DUP IF                    \ IF THERE IS SOMETHING ON STACK
      0 DO ACTUAL-RESULTS I CELLS + ! LOOP   \ SAVE THEM
   THEN
FLOATS? [IF]
   FDEPTH DUP ACTUAL-FDEPTH ! \ RECORD FDEPTH
   ?DUP IF                    \ IF THERE IS SOMETHING ON FSTACK
      0 DO ACTUAL-FRESULTS I FLOATS + F! LOOP   \ SAVE THEM
   THEN
[THEN]
;

: }T ( ... -- )
\G Compare stack (expected) contents with saved (actual) contents.
   DEPTH ACTUAL-DEPTH @ = IF  \ IF DEPTHS MATCH
      DEPTH ?DUP IF           \ IF THERE IS SOMETHING ON THE STACK
         0 DO                 \ FOR EACH STACK ITEM
            ACTUAL-RESULTS I CELLS + @    \ COMPARE ACTUAL WITH EXPECTED
            <> IF S" INCORRECT RESULT: " ERROR LEAVE THEN
         LOOP
      THEN
   ELSE                       \ DEPTH MISMATCH
      S" WRONG NUMBER OF RESULTS: " ERROR
   THEN
FLOATS? [IF]
   FDEPTH ACTUAL-FDEPTH @ = IF \ IF FDEPTHS MATCH
      FDEPTH ?DUP IF           \ IF THERE IS SOMETHING ON THE FSTACK
         0 DO                  \ FOR EACH FSTACK ITEM
            ACTUAL-FRESULTS I FLOATS + F@    \ COMPARE ACTUAL WITH EXPECTED
            FEQU? 0= IF S" INCORRECT FRESULT: " ERROR LEAVE THEN
         LOOP
      THEN
   ELSE                       \ DEPTH MISMATCH
      S" WRONG NUMBER OF FRESULTS: " ERROR
   THEN
[THEN]
;

: TALKING ( -- )
\G Set verbose mode for tester.
   TRUE VERBOSE ! ;

: TALKING? ( -- ff )
\G Return true if tester inn verbose mode.
   VERBOSE @ ;

: TESTING ( -- )
\G Talking comment for tester.
   SRCLINE DUP >IN +!
   VERBOSE @
   IF   ." TESTING " TYPE CR
   ELSE 2DROP [CHAR] * EMIT
   THEN ;


PREVIOUS
